(ns phel\schema
  (:use \Phel\Lang\Symbol)
  (:use \Phel\PhelSchema\ZodError)
  (:require phel\schema\interfaces :refer [schema schema? validate message])
  (:require phel\schema\type\number)
  (:require phel\schema\type\string)
  (:require phel\schema\type\object))

(defmacro- proxy [namespace symbols]
  `(do ,@(loop [symbols symbols
                acc []]
     (if (empty? symbols)
       acc
       (recur (rest symbols)
              (push
                acc
                (let [s (name (first symbols))]
                  `(defn ,(php/new Symbol nil s) [& args]
                     (apply ,(php/new Symbol namespace s) args)))))))))

(proxy "phel\schema\\type\string" [:string :max :min :length :regex])

(proxy "phel\schema\\type\\number" [:number :gt :gte :lt :lte :int :positive :nonnegative :negative :nonpositive])

(proxy "phel\schema\\type\object" [:object])

(declare get-issues)

(defn- get-parent-issues [schema paths target]
  (loop [rules (schema :rules)
         paths paths
         acc []]
    (if (empty? rules)
      acc
      (let [rule (first rules)]
        (recur (rest rules) paths (concat acc (validate rule paths target)))))))

(defn- get-children-issues [schema paths target]
  (reduce concat [] (for [[k v] :pairs (schema :children)]
                      (if (schema? v)
                        (get-issues v (push paths k) (target k))
                        []))))

(defn- get-issues [schema paths target]
  (concat
    (get-parent-issues schema paths target)
    (get-children-issues schema paths target)))

(defn safe-parse [schema target]
  (let [issues (get-issues schema [] target)]
    (if (empty? issues)
      {:success true
       :data target
       :issues []}
      {:success false
       :error (php/:: ZodError (create "Zod Error" (to-php-array issues)))
       :issues issues})))

(defn parse [schema target]
  (let [result (safe-parse schema target)]
    (if (result :success)
      (result :data)
      (throw (result :error)))))
